;          ************************************
;          *           USURPATOR II           *
;          *                                  *
;          * SCHAAKPROGRAMMA VOOR DE 6502.    *
;          * AUTEUR:  H.G.MULLER              *
;          * COPYRIGHT 1983                   *
;          ************************************
;
;***************************************************
;* OM DIT PROGRAMMA AAN TE PASSEN AAN EEN WILLEKEU-*
;* RIG 6502 SYSTEEM MOETEN DE MONITOR ADRESSEN     *
;* AANGEPAST WORDEN. LET OOK OP DE REGELS WAAR     *
;* COMMENTAAR ACHTER STAAT.   HET PROGRAMMA START  *
;* OP $200, EN LOOPT TOT $1400. HET GEBRUIKT DE    *
;* ZERO PAGE VOOR AL ZIJN VARIABELEN, TOT $DB,     *
;* EN PAGE 1 VOOR STACK RUIMTE.                    *
;* TIJDENS HET DENKEN GEBRUIKT HET BOVENDIEN NOG   *
;* WERKGEHEUGEN VAN $1400 TOT $1690.               *
;*                                                 *
;* OM TE SPELEN MOETEN WE OP $200 STARTEN; HET     *
;* PROGRAMMA VRAAGT DAARNA OM INPUT, EN WE KUNNEN  *
;* NU EEN ZET INTYPEN ALS HET BEGIN EN EINDVELD    *
;* ACHTER ELKAAR, B.V. E2E4. DE COMPUTER ZAL DAN   *
;* TESTEN OF DIT EEN LEGALE ZET IS, EN HEM IN DAT  *
;* GEVAL UITVOEREN. NONSENS ZETTEN WORDEN MET EEN  *
;* ? BEANTWOORD EN WE MOETEN HET DAN NOG EENS PRO- *
;* BEREN. BIJ ROKADE MOETEN WE DE KONINGS ZET IN-  *
;* TYPEN, B.V. E1G1, MAKEN WE HALVERWEGE HET IN-   *
;* VOEREN VAN DE ZET EEN TYPFOUT DAN KUNNEN WE .   *
;* INTYPEN WAARNA WE ? KRIJGEN EN OPNIEUW KUNNEN   *
;* PROBEREN.                                       *
;* ALS WE WILLEN DAT DE COMPUTER EEN ZET DOET,     *
;* (EN DIT KAN OP ELK MOMENT), MOETEN WE RETURN    *
;* INTOETSEN, WAARNA DE COMPUTER BEGINT TE DENKEN. *
;* HIERBIJ SPUIT HIJ ALLERLEI IRRELEVANTE INFORMA- *
;* TIE OP WAARAAN WE ZIEN DAT HIJ BEZIG IS.        *
;* (AANTAL AL BEKEKEN ZETTEN, ZET WAAROVER HIJ     *
;* DENKT, WAARDERERING BESTE ZET TOT NU TOE, DIEP- *
;* TE BOOM EN GEBRUIKTE TIJD.) ALS WE DIT NIET     *
;* WILLEN DAN MOETEN WE ALLE AANROEPEN VAN "STATUS"*
;* WEGHALEN. ALS HIJ UITGEDACHT IS PRINT HIJ:      *
;* NUMMER VAN DE ZET, DE ZET ZELF EN VRAAGT DAN    *
;* OM INPUT.                                       *
;* DE BEDENKTIJD KAN AANGEPAST WORDEN DOOR BUDGET  *
;* TE VERANDEREN; VOOR SNELSCHAAK IS 7 EEN LEUKE   *
;* WAARDE. ER MOETEN DAN OOK NOG 2 INSTRUCTIES     *
;* BIJ ADRES $213 WORDEN AANGEPAST.                *
;***************************************************
;
;***************************************************
;* PROGRAMMA PARAMETERS EN MONITOR ENTRYPOINTS.    *
;***************************************************
SCHAAK	=   $3900
MAT	=   -$2800
FMAT	=   -$2300
OSTK	=   $107
SAVS	=   $106
SAVT	=   $105
BUDGET	=   10
INALL	=   $E993		;INPUT 1 CHAR (IN ACC).
OUTALL	=   $E97A		;PRINT 1 CHAR (UIT ACC).
CRLF	=   $EA13		;PRINT CR + LF.
MONIT	=   $E1A1		;TERUG NAAR MONITOR.
BLANK	=   $E83E		;PRINT 1 SPATIE
NUMA	=   $EA46		;PRINT ACC ALS 2 HEX DIGITS.
WRAX	=   $EA42		; PRINT A (MSB) EN X ALS 4 HEX.
VIA	=   $A000
T1CL	=   VIA+4
ACR	=   VIA+11
IFR	=   VIA+13
IER	=   VIA+14
PRIFLG	=   $A411		;AIM PRINTER FLAG
TPZ	=   120			;TIJD (SEC) PER ZET
MSCHK	=   -SCHAAK
TIME	=   50000-2
;********************
;* VARIABELEN       *
;********************
	*=  0
BORD	*=  *+64
MARK	*=  *+64
ETYD	*=  *+2
MTYD	*=  *+6
KLEUR	*=  *+1
ZETTEN	*=  *+1
DEPTH	*=  *+1
COST	*=  *+1
MAXDEP	*=  *+1
MAXCOS	*=  *+1
STUK	*=  *+1
TOSTUK	*=  *+1
MODE	*=  *+1
KCNT	*=  *+1
TOX	*=  *+1
TOY	*=  *+1
PROCES	*=  *+2
ZET	*=  *+3
MAXVAL	*=  *+2
STAVAL	*=  *+2
DYNVAL	*=  *+2
LIMIT	*=  *+2
IPTR	*=  *+1
SV	*=  *+1
OLDSV	*=  *+1
JPTR	*=  *+1
RPTR	*=  *+1
BPTR	*=  *+1
TV	*=  *+1
OLDTV	*=  *+1
CLASS	*=  *+1
WPTR	*=  *+1
PPTR	*=  *+1
XCNT	*=  *+1
YCNT	*=  *+1
XSAV	*=  *+2
SUM	*=  *+2
RANDOM	*=  *+1
PASFLG	*=  *+1
MOB	*=  *+1
EINDSP	*=  *+1
LAST	*=  *+1
SPTR	*=  *+1
TMP	*=  *+1
EXTRA	*=  *+1
MVELD	*=  *+1
ALARM	*=  *+1
AGRES	*=  *+1
LINE	*=  *+1
OMODE	*=  *+1
NR	*=  *+1
PRISAV	*=  *+1
MOVDIR	*=  *+1
KPOS	*=  *+1
KFLG	*=  *+1
TICKS	*=  *+1
TNOD	*=  *+1
DFLG	*=  *+1
MOB2	*=  *+1
VFLG	*=  *+1
BONUS	*=  *+1
OKFLG	*=  *+1
PFLG	*=  *+1
KP1	*=  *+1
KP2	*=  *+1
CODE	*=  *+1
SFLG	*=  *+1
SST	*=  *+1
DREIG	*=  *+1
S2FLG	*=  *+1
BDT	*=  *+2
DEKCNT	*=  *+2
DEKFLG	*=  *+1
SDIF	*=  *+1
PANIK	*=  *+1
MFLG	*=  *+1
EZERO	=   *
;*************************************************
;* POSVAL BEVAT DE POSITIONELE WAARDERING VAN DE *
;* VELDEN OP HET SCHAAKBORD VOOR K, L, P EN PION.*
;*************************************************
	*=  $12C0
POSVAL	.BYTE 00,03,10,15,15,10,03,00
	.BYTE 05,20,25,28,28,30,20,05
	.BYTE 10,30,42,45,45,42,30,10
	.BYTE 15,35,45,50,50,45,35,15
	.BYTE 15,35,45,50,50,45,35,15
	.BYTE 10,30,42,45,45,42,30,10
	.BYTE 05,20,25,28,28,30,20,05
	.BYTE 00,03,10,15,15,10,03,00
	*=  $1640
NOTA	*=  *+64
TOREN	*=  *+8
;*************************************************
;* HOOFD PROGRAMMA.                              *
;* DOE EERST DE INITIALISATIE, EN GA DAARNA NAAR *
;* DE COMMANDO LOOP, DIE ERLP OM INPUT           *
;* VRAAGT. VOOR EEN WARME START (BIJVOORBEELD    *
;* NADAT WE MET DE MONITOR EEN STAND OP HET BORD *
;* HEBBEN GEZET OF GEWIJZIGD), KUNNEN WE BIJ     *
;* ERLP HET PROGRAMMA HERSTARTEN.                *
;* ALS ER EEN ZET IS INGEGEVEN DAN SPRINGEN WE   *
;* NAAR M1 OM HEM UIT TE VOEREN, ALS EEN RETURN  *
;* WERD GEGEVEN KOMEN WE BIJ M1A OM ZELF EEN ZET *
;* TE BEDENKEN.                                  *
;*************************************************
	*=  $200
	JSR INIT
MAIN	LDX #$FF
	TXS
LOOP	JSR WTYD
	LDA SV
	STA OLDSV
	LDA TV
	STA OLDTV
	LDA IPTR
	ORA #$80		;WEG BIJ SNELSCHAAK
	STA SV
	LDA BPTR
	ORA #$80		;WEG BIJ SNELSCHAAK
	STA TV
	LDX MOB
	LDA MOB2
	STA MOB
	STX MOB2
	INC NR
ERLP	JSR INLEES
	LDX T1CL		;HAAL RANDOM WAARDE UIT
	STX RANDOM		;LOW BYTE TIMER
	JSR MTMOB
	JSR KTEL
	LDA ZET
	BMI M1A
	JMP M1
;***************************************************
;* WE MOETEN ZELF EEN ZET DOEN, PLAATS DE SCHAAK   *
;* MERKTEKENS, EN KIJK HOEVEEL WE ACHTER STAAN.    *
;* LET ZOWEL OP AANTAL STUKKEN ALS WAARDE VERSCHIL,*
;* HET VERSCHIL WORDT BIJ ELKE SLAGZET GETELD, ALS *
;* BONUS, OM IN DIT SOORT SITUATIES AFRUILEN TEGEN *
;* TE GAAN. dAARNA GAAN WE BEPALEN HOE DIEP WE     *
;* GAAN DENKEN DOOR MAXCOS EEN WAARDE TE GEVEN,    *
;* AAN DE HAND VAN DE BEWEGELIJKHEID VAN DE BORD   *
;* SITUATIE, DIE IN MOB (VOOR ONS) EN MOB2 (VOOR   *
;* DE TEGENSTANDER) BEPAALD IS IN DE ROUTINE MTMOB.*
;* DAARNA ROEPEN WE "LDG" AAN OM EEN ZET TE VER-   *
;* ZINNEN, TESTEN OF DE PARTIJ TOEVALLIF IS AFGE-  *
;* LOPEN (ALS ZETTEN = 0), EN PRINTEN DE ZET OF    *
;* EEN BOODSCHAP.                                  *
;***************************************************
M1A	JSR WIS
	JSR EVAL
	LDA SDIF
	ASL
	CLC
	ADC SUM+1
	ASL
	ASL
	ASL
	STA BONUS
	LDA #0
	STA EINDSP
	STA TNOD
SPEED	LDA #BUDGET
	LDX MOB
	CPX #26
	BCS M5
	INC EINDSP
	LDY DFLG
	BNE M5C
	TAY
	INY
	TXA
	CLC
	ADC MOB2
	CMP #50
	TYA
	BCS M5C
	CLC
	ADC #2
M5C	LDY PFLG
	BEQ M5B
	LDY KFLG
	BNE M5
M5B	CPX #11
	BCC M5A
	LDX MOB2
	CPX #11
	BCS M5
M5A	ADC #2
M5	STA MAXCOS
	SEI
	LDA MTYD
	CMP ETYD
	LDA MTYD+1
	SBC ETYD+1
	CLI
	BCS NTN
	LDA #$80
	STA TNOD
	LDA MAXCOS
	SEC
	SBC #3
	STA MAXCOS
NTN	LDX #<DENK
	STX PROCES
	LDX #>DENK
	STX PROCES+1
	LDA PRIFLG		;SAVE AIM PRINTER TOESTAND
	STA PRISAV
	LDA #0
	STA PRIFLG		;ZET PRINTER UIT DENKS DENKE
	JSR CRLF		;NIEUWE REGEL
	JSR LDG
	JSR STATUS		;PRINTEN STATUS INFORMATIE
	LDA PRISAV		;HERSTEL PRINTER TOESTAND
	STA PRIFLG
	LDX DYNVAL
	LDY DYNVAL+1
	LDA ZETTEN
	BNE X0
	JMP EINDE
X0	CPX #<SCHAAK
	BNE X1
	CPY #>SCHAAK
	BNE X1
	JMP WIN
X1	LDA ZET
	STA IPTR
	LDA ZET+1
	STA BPTR
	STA LAST
	LDA ZET+2
	STA MODE
	STA OMODE
	JSR DOEZET
	JSR CRLF		;NIEUWE REGEL
	LDA NR
	LSR
	JSR NUMA		;PRINT NUMMER VAN DE ZET
	JSR BLANK		;PRINT SPATIE
	JSR PZET
	JSR BLANK		;PRINT SPATIE
BACK	JMP LOOP
;*************************************************
;* LEES EEN ZET IN VAN HET TOETSENBORD.          *
;* ALS EEN RETURN INGEGEVEN WORDT KEREN WE TERUG *
;* MET $FF, ANDERS LEZEN WE DE ZET IN, EN GEVEN  *
;* HEM AF IN ZET EN ZET+1.                       *
;*************************************************
INLEES	JSR INALL		;LEES 1E CHAR VAN INPUT
	CMP #$D
	BNE ILS1
	LDA #$FF
	STA ZET
	RTS
ILS1	JMP LZET
;**************************************************
;* TEST OF DE INGEGEVEN ZET LEGAAL WAS, (WAARBIJ  *
;* TEVENS BLIJKT OF HET EEN ROKADE WAS), DOOR     *
;* DE ZETTEN GENERATOR (SCAN2) AAN TE ROEPEN,     *
;* MET ZTST ALS PROCES. VOER DAARNA DE ZET UIT.   *
;**************************************************
M1	LDA #<ZTST
	STA PROCES
	LDA #>ZTST
	STA PROCES+1
	LDA #0
	STA OKFLG
	STA EXTRA
	JSR SCAN2
	LDA OKFLG
	BNE REG
	JMP ERR
REG	LDA ZET
	STA IPTR
	LDA ZET+1
	STA BPTR
	LDA ZET+2
	STA MODE
	STA OMODE
	JSR CRLF		;NIEUWE REGEL
	JSR DOEZET
	LDA BPTR
	STA LAST
	JMP LOOP
;*************************************************
;* EINDE VAN DE PARTIJ, ZOEK UIT OF HET MAT OF   *
;* PAT WAS, EN PRINT DE BIJBEHORENDE BOODSCHAP.  *
;*************************************************
EINDE	CPX #<FMAT
	BNE M2
	CPY #>FMAT
	BNE M2
	LDX #MES4-MES1
	BNE M3
M2	LDX #MES5-MES1
M3	JSR PRT
	JMP MONIT
WIN	LDX #MES6-MES1
	BNE M3
;******************************************
;* LEES ZET IN ALS BEGINVELD EN EINDVELD. *
;* EERSTE CHAR ZIT AL IN ACCUMULATOR.     *
;******************************************
LZET	JSR CK1
	STA ZET
	JSR CHECK
	STA ZET+1
	RTS
CHECK	JSR INALL		;LEES LETTER VAN VELD
CK1	SEC
	SBC #'A'
	BCC ERR
	CMP #8
	BCS ERR
	STA TMP
	JSR INALL		;LEES CIJFER VAN VELD
	SEC
	SBC #'1'
	BCC ERR
	CMP #8
	BCS ERR
	EOR #7
	ASL 
	ASL 
	ASL 
	ORA TMP
	RTS
ERR	LDX #MES3-MES1
	JSR PRT
	LDX #$FF
	TXS
	JMP ERLP
;****************
;* PRINT DE ZET *
;****************
PZET	LDA ZET
	JSR PVELD
	LDA ZET+1
PVELD	PHA
	AND #7
	CLC
	ADC #'A'
	JSR OUTALL		;PRINT LETTER VAN VELD
	PLA
	LSR
	LSR
	LSR
	EOR #7
	CLC
	ADC #'1'
	JMP OUTALL		;PRINT CIJFER VAN VELD
;************************************************
;* CONTROLEER IN GEVAL DEKFLG GEZET IS OF DE GE- *
;* SPEELDE ZET INDERDAAD HET ALARM STUK DEKT.    *
;* ZO NIET CANCEL DAN METEEN DEZE VARIANT.       *
;* ANDERS DAN GAAN WE VANUIT DEZE STAND DENKEN.  *
;*************************************************
SCAN0	LDA DEKFLG
	BEQ LDG
	LDX #<COMP
	STX PROCES
	LDX #>COMP
	STX PROCES+1
	LDY ALARM
	LDA #0
	STA (DEKCNT),Y
	STA DEKFLG
	LDX BPTR
	INX
	TXA
	DEX
	AND #$3F
	STA MVELD
	JSR WISSEL
	JSR NVELD
	JSR WISSEL
	LDX #<DENK
	STX PROCES
	LDX #>DENK
	STX PROCES+1
	LDY ALARM
	LDA (DEKCNT),Y
	BNE LDG
	DEC DEKFLG
	RTS
;*********************************************
;* DOE EEN SCHAAKTEST OF TE KIJKEN OF WE DE  *
;* KONING KUNNEN PAKKEN, DOOR DE ZETTENGENE- *
;* RATOR AAN TE ROEPEN MET STE1 ALS PROCES.  *
;* ZOEK TEVENS UIT MET WEL STUK WE MOETEN    *
;* BEGINNEN ZETTEN TE GENEREREN IN SST.      *
;* DOE DIT ALLES ALLEEN ALS HET DE MOEITE    *
;* LOONT. (= ALS ER NOG 3 BUDGET PUNTEN ZIJN)*
;*********************************************
LDG	LDA DREIG
	STA SST
	LDA COST
	CLC
	ADC #3
	CMP MAXCOS
	BCC VLG
	LDA EXTRA
	CMP #>MSCHK
	BEQ VLG
	LDA PASFLG
	BMI SCAN1
VLG	LDA #<STE1
	STA PROCES
	LDA #>STE1
	STA PROCES+1
	LDY #63
DKLP	LDA #0
	STA (DEKCNT),Y
	LDA BORD,Y
	AND #$BF
	STA BORD,Y
	DEY
	BPL DKLP
	JSR SCAN
	LDA #<DENK
	STA PROCES
	LDA #>DENK
	STA PROCES+1
	LDX MAXVAL+1
	CPX #>SCHAAK
	BNE SCAN1
	JMP CUTOFF
;*************************************************
;* DOE EEN VOORANALYSE OM TE KIJKEN OF DE LAATSTE*
;* ZET VAN DE TEGENSTANDER IETS AANVIEL, DOOR DE *
;* ZETTENGENERATOR AAN TE ROEPEN NA DE KLEUR     *
;* GEWISSELD TE HEBBEN, MET KIJK ALS PROCES.     *
;*************************************************
SCAN1	LDX #0
	STX EXTRA
	STX MFLG
	DEX
	STX ALARM
	LDA SFLG
	BEQ SZ
	LDX PASFLG
	INX
	BMI SCAN
	BPL SZET
SZ	LDA PASFLG
	BMI SCAN
SZET	LDA STUK
	AND #7
	CMP #6
	BEQ MD0
	CMP #3
	BCC MD0
	LDX JPTR
	LDA GRP,X
	LDX TOSTUK
	BEQ MD1
MD0	LDA #5
MD1	STA MOVDIR
	LDX #<KIJK
	STX PROCES
	LDX #>KIJK
	STX PROCES+1
	JSR WISSEL
	LDX TV
	INX
	TXA
	AND #$3F
	STA MVELD
	JSR SCAN2
	JSR WISSEL
	LDX #<DENK
	STX PROCES
	LDX #>DENK
	STX PROCES+1
	LDA MFLG
	BEQ PFP1
	LDA #0
	LDX EXTRA
	CPX #>SCHAAK
	BEQ PFP1
	LDA #$F0
PFP1	SEC
	SBC EXTRA
	STA EXTRA
SCAN	LDX #<MSCHK
	STX MAXVAL
	LDX #>MSCHK
	STX MAXVAL+1
	LDX SST
	STX MVELD
SCAN3	LDX #0
	STX ZETTEN
;*********************************************
;* ZETTEN GENERATOR:                         *
;* GENEREER ALLE ZETTEN VOOR DE AAN ZET      *
;* ZIJNDE PARTIJ (TE ZIEN AAN KLEUR).        *
;* ROEP VOOR ELKE GEVONDEN ZET DE ROUTINE    *
;* AANGEWEZEN DOOR PROCES AAN.               *
;* BEGIN HET BORD AF TE ZOEKEN NAAR DE EIGEN *
;* STUKKEN, TE BEGINNEN OP MVELD.            *
;* ALS GEEN ZETTEN GEVONDEN WORDEN, KIJK DAN *
;* OF WE SCHAAK STAAN, EN WARDEER MAT OF PAT.*
;*********************************************
SCAN2	LDX MVELD
NVELD	LDA BORD,X
	BEQ NOSTUK
	STA STUK
	AND #8
	CMP KLEUR
	BNE NOSTUK
	JMP MIJNST
NOSTUK	INX
	TXA
	AND #$3F
	TAX
	CPX MVELD
	BNE NVELD
	LDA ZETTEN
	BNE CUTOFF
	INC ZETTEN
	LDX #<STEST
	STX PROCES
	LDX #>STEST
	STX PROCES+1
	JSR WISSEL
	JSR SCAN2
	JSR WISSEL
	LDX #<DENK
	STX PROCES
	LDX #>DENK
	STX PROCES+1
	LDX #0
	STX ZETTEN
	LDX DYNVAL
	LDY DYNVAL+1
	CPX #<SCHAAK
	BNE PAT
	CPY #>SCHAAK
	BNE PAT
	LDX #<MAT
	LDY #>MAT
	JMP RET
PAT	JSR EVAL
	JMP RET
;*********************************************
;* WE ZIJN KLAAR, DE WAARDE VAN DE STELLING  *
;* ZIT IN MAXVAL. ALS DEZE NEGATIEF IS GEVEN *
;* WE EEN BONUS VOOR UITGESTELD VERLIES.     *
;*********************************************
CUTOFF	LDX MAXVAL
	LDY MAXVAL+1
RET	STX DYNVAL
	STY DYNVAL+1
	BIT DYNVAL+1
	BPL STS2
	TYA
	LSR
	ROR DYNVAL
	LSR
	ROR DYNVAL
	LSR
	ROR DYNVAL
	ORA #$E0
	TAY
	TXA
	SEC
	SBC DYNVAL
	STA DYNVAL
	LDA DYNVAL+1
	STY DYNVAL+1
	SBC DYNVAL+1
	STA DYNVAL+1
STS2	RTS
;**************************************************
;* KIJK OF WE DE KONING KUNNEN SLAAN. OOK OF WE   *
;* HET LAATST GEZETTE STUK KUNNEN SLAAN, WAT IN   *
;* SST GENOTEERD WORDT.                           *
;**************************************************
STE1	INC ZETTEN
	LDA TV
	AND #$3F
	CMP BPTR
	BNE STEST
	LDA IPTR
	STA SST
STEST	LDA TOSTUK
	AND #7
	CMP #6
	BNE STS1
	JMP BAD
STS1	RTS
;*******************
;* STRINGS         *
;*******************
MES1	.BYTE $D,$A,0
MES3	.BYTE '?',0
MES4	.BYTE " IK STA MAT",0
MES5	.BYTE " IK STA PAT",0
MES6	.BYTE " IK WIN!",0
;*****************************************************
;* WE HEBBEN EEN STUK GEVONDEN; GA NA IN WELKE RICH- *
;* TING DIT KAN ZETTEN IN DE RANGE TABEL             *
;*****************************************************
MIJNST	LDA STUK
	AND #7
	ASL
	ASL
	ASL
	ASL
	CLC
	ADC #$F
	STA RPTR
	LDA #$F
	STA JPTR
	STX IPTR
NDIR	LDX RPTR
	LDA RANGE,X
	BEQ BREAK
	BPL VALDIR
	EOR #$FF
	STA RPTR
	AND #$F
	STA JPTR
BREAK	DEC RPTR
	DEC JPTR
	BPL NDIR
	LDX IPTR
	JMP NOSTUK
;***********************************************
;* WE HEBBEN EEN RICHTIN GEVONDEN WAARIN HET   *
;* STUK MAG ZETTEN. LOOP DEZE RICHTING AF,     *
;* KCNT GEEFT AAN HOEVER WE NOG MOGEN,         *
;* BPTR BEVAT HET DOELVELD                     *
;* DE ZET WORDT TEVENS GECLASSIFICEERD, ALS    *
;* TERUGSLAAN, PENNING, AFTREKJE OF VERDER     *
;* SLAAN, OF COMBINATIES HIERVAN, IN CLASS.    *
;* DE VFLG BUSINESS DIENT OM 2 GRATIS DAMEZET- *
;* TEN ACHTER ELKAAR TE VOORKOMEN.             *
;***********************************************
VALDIR	STA MODE
	AND #$F
	STA KCNT
	LDA VFLG
	CMP DEPTH
	BCC NL3
	LDA #0
	STA VFLG
NL3	LDY IPTR
	CPY OLDTV
	BNE L1
	LDA STUK
	AND #7
	CMP #5
	BNE NL1
	LDA VFLG
	BNE L1
	LDA DEPTH
	STA VFLG
NL1	LDA #2
	JMP NL2
L1	LDA #0
NL2	STA CLASS
	TYA
	AND #7
	STA TOX
	EOR IPTR
	STA TOY
;*************************************************
;* GENEREER VOLGENDE VELD VAN DE LIJN, EN KIJK   *
;* OF HET OP HET VORD LIGT. ALS ER WAT STAAT VAN *
;* DE TEGENSTANDER DAN IS DE ZET LEGAAL. SOM WOR-*
;* DEN OOK NIET LEGALE ZETTEN OPGEWEKT, TEN BE-  *
;* HOEVE VAN KIJK EN STE1. IN HET ARRAY DEKCNT   *
;* WORDT BIJGEHOUDEN HOEVAAK ALLES AANGEVALLEN/  *
;* GEDEKT STAAT. (VERDUBBELING MEEGETELT!) ALS   *
;* WE IN KIJK OF STE1 ZITTEN.                    *
;*************************************************
NZET	LDX JPTR
	LDA TOX
	CLC
	ADC XDIR,X
	STA TOX
	AND #$F8
	BNE BREAK
	LDA TOY
	CLC
	ADC YDIR,X
	STA TOY
	AND #$C7
	BNE BREAK
	LDA TOX
	ORA TOY
	STA BPTR
	TAX
	LDA BORD,X
	STA TOSTUK
	LDX PROCES
	CPX #<DENK
	BEQ L4D
	CPX #<ZTST
	BEQ L4D
	LDA MODE
	AND #$10
	BNE L4D
	LDY #1
	LDA STUK
	AND #7
	CMP #2
	BCS L4G
	LDX BPTR
	LDA BORD,X
	BEQ L4G
	PHA
	ORA #$40
	STA BORD,X
	PLA
	AND #$40
	BNE L4H
	LDA TOSTUK
	AND #7
	CMP #2
	BCS L4G
L4H	LDY #3
L4G	STY TMP
	LDX PROCES
	LDY BPTR
	LDA (DEKCNT),Y
	CPX #<KIJK
	BNE L4E
	CLC
	ADC TMP
	JMP L4F
L4E	SEC
	SBC TMP
L4F	STA (DEKCNT),Y
	LDA TOSTUK
	AND #8
	CMP KLEUR
	BNE L4D
	EOR TOSTUK
	ASL
	ASL
	ASL
	ASL
	CMP #$60
	BEQ L4D
	CLC
	ADC JPTR
	TAX
	LDA RANGE,X
	BEQ L4D
	BMI L4D
	DEC KCNT
	BEQ LBR
	STA MODE
	AND #$F
	CMP KCNT
	BCS JNZ
	STA KCNT
JNZ	JMP NZET
L4D	LDY CLASS
	LDA TOSTUK
	BNE L5A
	LDA #$20
	BIT MODE
	BEQ L4C
	JMP GEP
L5A	AND #8
	CMP KLEUR
	BNE L4A
LBR	JMP BREAK
L4A	INY
	LDA #$10
	BIT MODE
	BNE LBR
L4C	TYA
	LDY BPTR
	CPY SV
	BNE L2
	ORA #4
L2	CPY OLDSV
	BNE L3
	ORA #8
L3	CPY TV
	BNE L4B
	ORA #$10
L4B	STA CLASS
;*************************************************
;* WE HEBBEN EEN ZET GEVONDEN, ROEP NU HET PRO-  *
;* CES AAN DAT WE ERMEE UIT WILLEN VOEREN, VIA   *
;* INDIR. ALS HET GEEN SLAGZET WAS GAAN WE DOOR. *
;* ALS HET STUK AAN HET EIND VAN ZIJN BEREIK IS  *
;* (KCNT!) DAN STOPPEN WE. UITZONDERINGEN VOOR   *
;* PIONNEN OP 2E RIJ, ROKADE, E.P. SLAAN WORDEN  *
;* HIER GETEST EN BEHANDELD.                     *
;*************************************************
	JSR INDIR
	LDA CLASS
	LSR
	BCS L7
	DEC KCNT
	BNE NZ
	INC KCNT
	LDA RPTR
	LDY TOY
	CMP #3
	BNE L6
	CPY #$28
	BNE L7
NZ	JMP NZET
L6	CMP #$11
	BNE L7A
	CPY #$10
	BEQ NZ
L7	JMP BREAK
L7A	LDA MODE
	AND #$40
	BEQ L7
	LDA STUK
	BPL L7
	LDA EXTRA
	CMP #>MSCHK
	BEQ L7
	JSR HOEK
	LDA BORD,X
	BPL L7
	TXA
	EOR #1
	TAX
	LDA BORD,X
	BNE L7
	LDA MODE
	AND #$BF
	ORA #$90
	STA MODE
	JMP NZET
GEP	INY
	LDA TV
	AND #$7F
	TAX
	LDA BORD,X
	EOR STUK
	CMP #9
	BNE L7
	LDA SV
	CLC
	ADC TV
	LSR
	TAX
	EOR SV
	AND #7
	BNE L7
	CPX BPTR
	BNE L7
	LDA MODE
	ORA #$88
	STA MODE
	JMP L4C
INDIR	JMP (PROCES)
;*************************************************
;* DEZE ROUTINE DOET HET EINGENLIJKE DENKWERK.   *
;* ILLEGALE ROKADES WORDEN EERST ONDERSCHEPT.    *
;* NA GEKEKEN TE HEBBEN OF ZETTENHERHALING OP-   *
;* TREEDT, BESLISSEN WE AAN DE HAND VAN DE       *
;* KOSTEN VAN DE ZET (OPGEZOCHT IN PRICE AAN DE  *
;* HAND VAN CLASS) OF WE HEM WILLEN BEKIJKEN.    *
;* IS DIT NIET HET GEVAL DAN WORDT DE ZET GEWAAR-*
;* DEERD MET DE WAARDERING EXTRA, DIE UIT DE     *
;* VOORANALYSE VERKREGEN IS. mEESTAL ZAL DIT NUL *
;* ZIJN, TENZIJ DE LAATSTE ZET VAN DE TEGENSTAN- *
;* DER IETS AANVIEL. IN DAT GEVAL BEKIJKEN WE    *
;* ALTIJD DE ZETTEN DIE HET STUK MOGELIJK REDDEN,*
;* ZONDER DAT COST OPGEHOOGD WORDT. BIJ L9H      *
;* WORDT DIT UITGEZOCHT. WE DOEN DIT TEN HOOGSTE *
;* 1 MAAL, GECONTROLEERD DOOR PASFLG. ALLEEN ALS *
;* DE KONING SCHAAK STAAT MAG HET NOG EEN TWEEDE *
;* MAAL GEDAAN WORDEN.                           *
;*************************************************
DENK	LDA TOSTUK
	AND #7
	CMP #6
	BEQ BAD
	LDA OMODE
	AND #$88
	CMP #$80
	BNE GOOD
	LDA SV
	CLC
	ADC TV
	LSR
	CMP BPTR
	BNE GOOD
BAD	LDX #<SCHAAK
	LDY #>SCHAAK
	STX MAXVAL
	STY MAXVAL+1
	JMP L12
GOOD	LDA OLDSV
	AND #$3F
	CMP BPTR
	BNE PROC
	LDA OLDTV
	AND #$3F
	CMP IPTR
	BNE PROC
	TSX
	LDA SAVT,X
	AND #$3F
	CMP SV
	BNE PROC
	LDA SAVS,X
	AND #$3F
	CMP TV
	BNE PROC
	JSR EVAL
L9A	STX STAVAL
	STY STAVAL+1
	JMP L8A
;*************************************************
;* NU GAAN WE UITZOEKEN OF WE DE ZET WILLEN DOEN.*
;* HET CRITERIUM HIERVOOR IS DAT DE ZET NIET     *
;* MEER MAG KOSTEN ALS MAXCOS AANGEEFT. DE PRIJS *
;* VAN EEN ZET VAN TYPE CLASS WORDT IN DE REGEL  *
;* UIT PRICE GEHAALD, MAAR UITZONDERINGEN WORDEN *
;* GEMAAKT VOOR PIONEN OP DE 6E RIJ (PANIK), EN  *
;* SCHAAKZETTEN, DIE ALS SLAAN TELLEN (SOMS).    *
;* ALS DE ZET BETAALBAAR IS GAAN WE NAAR L9 OM   *
;* VOOR DE TEGENSTANDER TE DENKEN NA DEZE ZET.   *
;* ALS WE SCHAAK STAAN, WAT TE ZIEN IS AAN EXTRA *
;* (HIGH BYTE HSCHK) VERANDERD DE VOLGORDE VAN   *
;* DE TESTS, OMDAT HET GEEN ZIN HEEFT ZETTEN TE  *
;* BEKIJKEN DIE HET SCHAAK NIET OPHEFFEN.        *
;*************************************************
PROC	LDA DEPTH
	BNE L8B
	JSR STATUS		;PRINT STATUS INFORMATIE
L8B	LDA #0
	STA S2FLG
	STA SFLG
	STA DEKFLG
	LDX DEPTH
	CPX PANIK
	BCS L8C
	STA PANIK
L8C	LDA EXTRA
	CMP #>MSCHK
	BNE L903
	JMP L9H
L903	LDA STUK
	JSR PTB
	BNE L901
	LDY TOY
	CPY #8
	BEQ L902
	CPY #$30
	BNE L901
L902	LDX PANIK
	BNE L901
	LDX DEPTH
	INX
	STX PANIK
	LDA STUK
	ORA #$10
	LDY IPTR
	STA BORD,Y
	JMP L9M
L901	TAX
	LDA SCOD,X
	LDX BPTR
	AND MARK,X
	BEQ L9I
	LDX TV
	LDA BORD,X
	AND #7
	CMP #6
	BNE WS2
	LDA EINDSP
	BNE WS2
WS1	INC S2FLG
WS2	LDX PASFLG
	INX
	BMI L9I
	INC SFLG
	LDA STUK
	AND #7
	CMP #2
	BEQ WS3
	LDA EINDSP
	BEQ WS3
	TSX
	LDA $114,X
	CMP #>MSCHK
	BNE L9I
	LDA $116,X
	CMP IPTR
	BNE WS3
	LDY JPTR
	LDA XDIR,Y
	CLC
	ADC YDIR,Y
	CMP $117,X
	BNE L9I
WS3	LDY BPTR
	LDX PASFLG
	BPL WS4
	LDA (DEKCNT),Y
	CMP #$FF
	BNE WS4
	LDX BPTR
	CPX SV
	BEQ L9I
WS4	LDA (DEKCNT),Y
	BPL L9I
WSK	LDA CLASS
	ORA #1
	TAX
	JMP L9I1
L9I	LDX CLASS
L9I1	LDA COST
	CLC
	ADC PRICE,X
	CMP MAXCOS
	BCS L9P
	JMP L9
L9P	LDA EXTRA
	CMP #>MSCHK
	BNE L9J1
	LDX PASFLG
	INX
	BPL L9J
	JMP L8
L9J1	LDX PASFLG
	BMI L9N8
	CMP #$F1
	BMI L9E
;*************************************************
;* DE ZET IS TE DUUR, MAAR PASFLG GEEFT AAN DAT  *
;* DIT DE 1E KEER IS. KIJK DAAROM OF ER PROBLE-  *
;* MEN ZIJN (ALARM), EN ALS DE GEGEVEN ZET TOE-  *
;* VALLIG DEZE PROBLEMEN BESTRIJDT DOE HEM DAN   *
;* TOCH, MAAR ZET PASFLG OM HERALING TE VOORKO-  *
;* MEN (L9J). WEGZETTEN, TUSSENZETTEN EN SLAAN   *
;* AANVALLER WORDEN OP DEZE MANIER HERKEND.      *
;* DEKKEN IS IETS MOEILIJKER: ALS DIT POTENTIEEL *
;* HELPT, (WAT REEDS IN KIJK GECONSTATEERD IS),  *
;* DAN GAAN WE KIJKEN OF DE LAATSTE ZET HET STUK *
;* IN MOEILIJKHEDEN DEKT. DIT IS NIET ALTIJD MET-*
;* EEN TE ZIEN; ZET IN ZO'N GEVAL DEKFLG EN PRO- *
;* BEER HET UIT; BIJ SCAN0 WORDT DIT DAN GETEST. *
;*************************************************
L9H	LDA ALARM
	BMI L9N8
	CMP IPTR
	BEQ L9E
	LDA AGRES
	AND #$3F
LLP	CMP BPTR
	BEQ L9E
	CLC
	ADC LINE
	CMP ALARM
	BNE LLP
	JMP L9C
L9E	LDA EXTRA
	CMP #>MSCHK
	BEQ L9I
L9J	DEC PASFLG
	JSR STAT
	LDA COST
	JSR DYNAM
	INC PASFLG
	LDA DEKFLG
	BPL L9D1
	INC DEKFLG
	JMP L9N
L9D1	JMP L9D
L9C	LDA EXTRA
	CMP #>MSCHK
	BNE L9N1
	JMP L8
L9N8	JMP L9N
L9N1	LDA AGRES
	BPL L9N
	LDY ALARM
	LDA STUK
	AND #7
	TAX
	CMP #2
	BCC L9N2
	LDA (DEKCNT),Y
	CMP #1
	BNE L9N
L9N2	TYA
	AND #7
	SEC
	SBC TOX
	STA TMP
	BEQ TD
	TYA
	SEC
	SBC TOY
	LSR
	LSR
	LSR
	BEQ TD
	STA XSAV
	AND #$10
	BEQ L9N7
	LDA #$E0
L9N7	ORA XSAV
	TAY
	CMP TMP
	BEQ DL
	CLC
	ADC TMP
	BEQ DL
	CPX #2
	BNE L9N
	LDX #10
L9N4	STX TMP
	LDA LAST
	PHA
	LDA BPTR
	STA LAST
	LDA ALARM
	JSR DIF
	TAX
	PLA
	STA LAST
	CPX TMP
	BCS L9N
	JMP L9J
L9N6	TXA
	ASL
	SEC
	SBC #1
	STY TMP
	CMP TMP
	BNE L9N
	JMP L9J
TD	LDA #4
	BNE L9N3
DL	CPX #2
	BCC L9N6
	LDA #2
L9N3	AND SCOD,X
	BEQ L9N5
	INC DEKFLG
	JMP L9J
L9N5	CPX #6
	BEQ L9N4
;*********************************************
;* IN HET EINDSPEL WORDT OOK ALTIJD GEDACHT  *
;* OVER PIONNEN PROMOTIES!                   *
;* VERDER ZETTEN MET DEZELFDE PION IS GRATIS,*
;* EN ER NAAR TOE HOLLEN MET KONING EN PAARD *
;* OOK. EEN ZET DIE GEEN POGING DOET EEN LO- *
;* PENDE PION TE STOPPEN WORDT GEWAARDEERD   *
;* ALSOF HET IN EEN PROMOTIE ZAL RESULTEREN. *
;*********************************************
L9N	LDA KFLG
	BEQ L9F
	LDA OLDTV
	CMP IPTR
	BNE KR1
	LDA STUK
	JSR PTB
	BEQ L9M1
KR1	LDX TV
	JSR PT2
	BNE L9F
	LDA STUK
	AND #7
	CMP #6
	BEQ DTST
	CMP #3
	BCS L9F
	BNE L9R
DTST	LDA LAST
	PHA
	LDA TV
	ASL
	SEC
	SBC SV
	STA LAST
	LDA BPTR
	JSR DIF
	STA TMP
	LDA TV
	STA LAST
	LDA IPTR
	JSR DIF
	TAX
	PLA
	STA LAST
	CPX TMP
	BCS L9M2
L9R	LDY #$F8
	JMP L9Q
;*************************************************
;* KIJK OOK ALTIJD NAAR PION PROMOTIES, EN PION  *
;* VORKJES (BUIT). ALS DE ZET TENSLOTTE HELEMAAL *
;* NIET BEKEKEN WORDT DAN WAARDEREN WE HEM BIJ   *
;* L9Q STATISCH AAN DE HAND VAN DEKCNT ALS DEZE  *
;* TENMINSTE BIJGEHOUDEN WAS (TE ZIEN AAN PASFLG)*
;*************************************************
L9F	LDA STUK
	JSR PTB
	BNE L9O
	LDA TOY
L9M1	BEQ L9M
	CMP #$38
	BEQ L9M
	LDA TOX
	BEQ L9O
	CMP #7
	BEQ L9O
	LDA KLEUR
	EOR #8
	ASL
	SEC
	SBC #8
	CLC
	ADC BPTR
	TAX
	DEX
	JSR BUIT
	BCC L9O
	INX
	INX
	JSR BUIT
L9M2	BCS L9M
L9O	LDX DEPTH
	CPX PANIK
	BEQ L9M
	LDA MODE
	BPL L9Q1
	LDY EXTRA
	LDX #15
	JMP L9A
L9Q1	LDY EXTRA
L9Q	LDX S2FLG
	BEQ L9Q2
	LDX DFLG
	BEQ L9Q3
	INY
L9Q3	LDX #$C0
L9Q2	LDA PASFLG
	BMI L9Q6
	STY TMP
	STX XSAV
	LDY BPTR
	LDA TOSTUK
	JSR WAARDE
	BEQ L9Q5
	CLC
	ADC TMP
	STA TMP
L9Q4	LDA (DEKCNT),Y
	BMI L9Q5
	LDA STUK
	JSR WAARDE
	EOR #$FF
	SEC
	ADC TMP
	STA TMP
L9Q5	LDY TMP
	LDX XSAV
L9Q6	JMP L9A
L9M	LDA COST
L9	PHA
	JSR STAT
	PLA
	JSR DYNAM
	LDX IPTR
	LDA STUK
	STA BORD,X
L9D	LDX DYNVAL
	LDY DYNVAL+1
	CPX #<SCHAAK
	BNE X2
	CPY #>SCHAAK
	BEQ L8
;****************************************************
;* BEREKEN NU DE TOTALE WAARDERING VAN DE ZET ALS   *
;* STATISCH MIN DYNAMISCH (STAVAL-DYNVAL). ALS DEZE *
;* GROTER IS ALS DE BESTE TOT NU TOE (MAXVAL) HOU-  *
;* DEN WE DIT BIJ, EN KUNNEN BIJ EEN ZET WAARMEE WE *
;* TEVREDEN ZIJN (BEPAALD DOOR LIMIT) ZELFS STOPPEN.*
;* OP DE 1E PLY (DEPTH=0) ONTHOUDEN WE OOK DE ZET.  *
;****************************************************
X2	SEC
	LDA STAVAL
	SBC DYNVAL
	STA STAVAL
	LDA STAVAL+1
	SBC DYNVAL+1
	STA STAVAL+1
L8A	INC ZETTEN
	LDA STAVAL
	LDY STAVAL+1
	CPY MAXVAL+1
	BMI L8
	BNE L10
	CMP MAXVAL
	BCC L8
	BEQ L8
L10	STA MAXVAL
	STY MAXVAL+1
	CPY LIMIT+1
	BMI L11
	BNE L12
	CMP LIMIT
	BCS L12
L11	LDA EXTRA
	CMP #>MSCHK
	BEQ L11A
	CMP #$F1
	BPL L11B
	CLC
	ADC #16
	CMP STAVAL+1
	BPL L11A
	STA EXTRA
L11B	TYA
	BMI L11A
	LDA #$FF
L11C	STA ALARM
L11A	LDA DEPTH
	BNE L8
	LDA BPTR
	STA ZET+1
	LDA IPTR
	STA ZET
	LDA MODE
	STA ZET+2
L8	RTS
L12	PLA
	PLA
	LDA IPTR
	STA DREIG
	JMP CUTOFF
;****************************************************
;* DYNAM WAARDEERT DE STELLING DIE ONSTAAT NADAT    *
;* WE ONZE ZET DOEN, DOOR VOOR DE TEGENSTANDER TE   *
;* GAAN DENKEN. DE WAARDEREING WORDT AFGEGEVEN IN   *
;* DYNVAL. EERST MOET DE GEHELE TOESTAND VAN DE     *
;* ZETTENGEERATOR OP DE STACK GESAVED WORDEN,       *
;* VANWEGE STACKOVERFLOW KAN DIT SLECHTS 9 PLIES.   *
;* BOVENDIEN BEREKENEN WE VIA HET ALFA-BETA ALGO-   *
;* RITHME DE NIEUWE WAARDE WAARBIJ DE ZET ALS AFGE- *
;* STRAFT BESCHOUWD KAN WORDEN. (BIJ ZL1 ETC.)      *
;****************************************************
OVFLW	LDX #0
	STX DYNVAL
	STX DYNVAL+1
	RTS
DYNAM	LDY DEPTH
	INY
	CPY MAXDEP
	BCS OVFLW
	STA TMP
	CPY #2
	BCC ZL1
	TSX
	LDA $110,X
	SEC
	SBC $112,X
	STA XCNT
	LDA $10F,X
	SBC $111,X
	STA YCNT
	BPL NB1
	LDX XCNT
	LSR
	ROR XCNT
	LSR
	ROR XCNT
	ORA #$C0
	PHA
	TXA
	CLC
	ADC XCNT
	STA XCNT
	PLA
	ADC YCNT
	STA YCNT
NB1	CMP MAXVAL+1
	BMI ZL1
	BNE ZL2
	LDA XCNT
	CMP MAXVAL
	BCS ZL2
ZL1	LDA MAXVAL
	STA XCNT
	LDA MAXVAL+1
	STA YCNT
ZL2	LDA STAVAL
	SEC
	SBC XCNT
	STA XCNT
	LDA STAVAL+1
	SBC YCNT
	STA YCNT
	LDX TMP
	LDA PASFLG
	BPL NCT
	LDA KFLG
	ORA SFLG
	BNE NCT
	TXA
	CLC
	ADC #4
	CMP MAXCOS
	BCC NCT
	LDA YCNT
	BPL NCT
	JMP OVFLW
NCT	STY DEPTH
	LDA COST
	PHA
	STX COST
	JSR DOEZET
	LDA DEKCNT
	CLC
	ADC #64
	STA DEKCNT
	BCC NDK
	INC DEKCNT+1
NDK	LDX BPTR
	LDY IPTR
	JSR UPDAT
	LDA MVELD
	PHA
	LDA LINE
	PHA
	LDA AGRES
	PHA
	LDA ALARM
	PHA
	LDA EXTRA
	PHA
	LDA OMODE
	PHA
	LDA MODE
	STA OMODE
	LDA RPTR
	PHA
	LDA CLASS
	PHA
	LDA LIMIT
	PHA
	LDA LIMIT+1
	PHA
	LDA STAVAL
	PHA
	LDA XCNT
	STA LIMIT
	LDA STAVAL+1
	PHA
	LDA YCNT
	STA LIMIT+1
	LDA MAXVAL
	PHA
	LDA MAXVAL+1
	PHA
	LDA ZETTEN
	PHA
	LDA KCNT
	PHA
	LDA STUK
	PHA
	LDA TOSTUK
	PHA
	LDA OLDSV
	PHA
	LDA SV
	STA OLDSV
	LDA IPTR
	STA SV
	LDA OLDTV
	PHA
	LDA TV
	STA OLDTV
	LDA BPTR
	STA TV
	JSR SCAN0
	LDA TV
	STA BPTR
	AND #7
	STA TOX
	EOR BPTR
	STA TOY
	LDA OLDTV
	STA TV
	PLA
	STA OLDTV
	LDA SV
	STA IPTR
	LDA OLDSV
	STA SV
	PLA
	STA OLDSV
	PLA
	STA TOSTUK
	PLA
	STA STUK
	PLA
	STA KCNT
	PLA
	STA ZETTEN
	PLA
	STA MAXVAL+1
	PLA
	STA MAXVAL
	PLA
	STA STAVAL+1
	PLA
	STA STAVAL
	PLA
	STA LIMIT+1
	PLA
	STA LIMIT
	PLA
	STA CLASS
	PLA
	STA RPTR
	AND #$F
	STA JPTR
	LDA OMODE
	STA MODE
	PLA
	STA OMODE
	PLA
	STA EXTRA
	PLA 
	STA ALARM
	PLA
	STA AGRES
	PLA
	STA LINE
	PLA
	STA MVELD
	LDA DEKCNT
	SEC
	SBC #64
	STA DEKCNT
	BCS WDK
	DEC DEKCNT+1
WDK	JSR ONTDOE
	LDX IPTR
	LDY BPTR
	JSR UPDAT
	PLA
	STA COST
	DEC DEPTH
	RTS
;**************************************************
;* VOER DE ZET VAN IPTR NAAR BPTR VAN TYPE MODE   *
;* UIT OP HET BORD. SECIALE GEVALEN VOOR E.P.,    *
;* ROKADE EN PROMOTIE (ALTIJD NAAR DAME).         *
;**************************************************
DOEZET	LDA MODE
	BPL DZ3
	AND #8
	BEQ ROK
EP	LDA TV
	AND #$7F
	TAX
	LDA #0
	STA BORD,X
	JMP DZ3
ROK	JSR HOEK
	LDA BORD,X
	STA BORD,Y
	LDA #0
	STA BORD,X
DZ3	LDX IPTR
	LDY BPTR
	JSR PT2
	BNE DZ1
	LDA BORD,X
	AND #$1F
	CPY #8
	BCC DZ2
	CPY #$38
	BCC DZ1
DZ2	ORA #5
	AND #$EF
DZ1	STA BORD,Y
	JSR KPT
	LDA #0
	STA BORD,X
WISSEL	LDA KLEUR
	EOR #8
	STA KLEUR
	LDA #0
	SEC
	SBC BONUS
	STA BONUS
	RTS
;***********************************************
;* NEEM DE ZET IN IPTR EN BPTR TERUG. SPECIAAL *
;* GEVAL VOOR E.P. SLAAN EN ROKADE.            *
;***********************************************
ONTDOE	LDA MODE
	BPL OD1
	AND #8
	BEQ ONTROK
ONTEP	LDA TV
	AND #$7F
	TAX
	LDA STUK
	EOR #9
	STA BORD,X
	JMP OD1
ONTROK	JSR HOEK
	LDA BORD,Y
	STA BORD,X
	LDA #0
	STA BORD,Y
OD1	LDX BPTR
	LDY IPTR
	LDA TOSTUK
	STA BORD,X
	LDA STUK
	STA BORD,Y
	JSR KPT
	JMP WISSEL
HOEK	LDA IPTR
	CLC
	ADC BPTR
	LSR
	TAY
	SEC
	SBC BPTR
	LSR
	AND #7
	EOR BPTR
	AND #7
	EOR BPTR
	TAX
	RTS
;*********************************************************
;* BEREKEN TOTALE STAND. RESULTAAT IN STAVAL.            *
;*********************************************************
EVAL	LDX #0
	STX SUM
	STX SUM+1
E1	LDA BORD,X
	STX XSAV
	JSR WAARDE
	LDX XSAV
	LDA BORD,X
	AND #8
	CMP KLEUR
	BNE E2
	SEC
	LDA SUM
	SBC STAVAL
	STA SUM
	LDA SUM+1
	SBC STAVAL+1
	JMP E3
E2	CLC
	LDA SUM
	ADC STAVAL
	STA SUM
	LDA SUM+1
	ADC STAVAL+1
E3	STA SUM+1
	INX
	CPX #64
	BNE E1
	LDX SUM
	LDY SUM+1
	RTS
;*********************************************************
;* TEST OF OP AANGEGEVEN VELD EEN EIGEN PION STAAT.
;*********************************************************
PTST	CLC
	ADC YCNT
PT0	TAX
PTA	LDA BORD,X
	AND #8
	CMP KLEUR
	BNE PT1
PT2	LDA BORD,X
PTB	AND #$F
	CMP #8
	BEQ PT1
	CMP #1
PT1	RTS
;*************************************************************
;* STAT BEREKENT DE STATISCHE WAARDERING VAN DE ZET IN IPTR  *
;* EN BPTR. WAARDE WORDT AFGELEVERD IN STACAL. VOOR ROKADE   *
;* EN E.P. SLAAN HERKENNEN WE APARTE GVALLEN, EN GAAN RESP.  *
;* NAAR WROK EN WEP. DE WAARDERING BESTAAT UIT:              *
;* WAARDE VAN HET GESLAGEN STUK (VIA WAARDE)                 *
;* CENRUM WAARDERING (UIT POSVAL) VOOR PION, L, P EN K       *
;* DUBBELPION EN ZWAKKE VELDEN WAARDERING VOOR PIONNEN       *
;* STRAFPUNTEN VOOR ZETTEN MET KONING                        *
;* IN HET EINDSPEL PUNTEN OP KONING NAAR ACTIE TOE TE ZETTEN *
;* GAAN NAAR LIJN MET WEINIG PIONNEN VOOR TORENS.            *
;* STRAFPUNTEN VOOR TWEE MAAL ACHTEREEN ZETTEN STUK.         *
;* EVT. WAARDE DAME BIJ PROMOTIE.                            *
;* BONUS/STRAF VOOR GESLAGEN STUKKEN I.V.M. AFRUILEN         *
;* RANDOM COMPONENT VOOR KEUZE TUSSEN GELIJKE ZETTEN         *
;*************************************************************
STAT	LDA MODE
	BPL GST
	AND #8
	BEQ WROK
WEP	LDA #1
	JMP GST2
WROK	LDY #$FF
	LDA BPTR
	EOR #$10
	JSR PT0
	BNE WR0
	INY
	INY
WR0	TXA
	EOR #$18
	JSR PT0
	BNE WR1
	INY
	INY
	INY
WR1	INX
	JSR PTA
	BNE WR2
	INY
WR2	DEX
	DEX
	JSR PTA
	BNE WR3
	INY
	INY
WR3	TYA
	STY TMP
	ASL
	ASL
	CLC
	ADC TMP
	ASL
	STA TMP
	LDA #0
	STA STAVAL+1
	JMP ST3
GST	LDA TOSTUK
GST2	JSR WAARDE
	LDA STUK
	AND #7
	TAX
	LDA #0
	CPX #6
	BNE ST0
	LDX EINDSP
	BEQ ST1A
	LDA BPTR
	JSR DIF
	STA TMP
	LDA IPTR
	JSR DIF
	SEC
	SBC TMP
	ASL
	ASL
	JMP ST1
ST1A	SEC
	SBC #20
AST1	JMP ST1
ST0	CPX #4
	BCC ST2A
	BNE ST2E
	LDX TOSTUK
	BNE ST2F
	STA TMP
	LDA IPTR
	EOR BPTR
	AND #$20
	BEQ ST2C
	LDA KLEUR
	ASL
	ASL
	EOR BPTR
	AND #$20
	BEQ ST2D
	LDA #25
	BNE ST2C
ST2D	LDA #$100-25
ST2C	CLC
	ADC TMP
	TAY
	LDA IPTR
	AND #7
	TAX
	TYA
	SEC
	SBC TOREN,X
	TAY
	LDA BPTR
	AND #7
	TAX
	TYA
	CLC
	ADC TOREN,X
	JMP ST2F
ST2E	CPX #5
	BNE ST2F
	LDX NR
	CPX #8
	BCS ST2F
	SEC
	SBC #15
ST2F	JMP ST2
ST2A	CPX #2
	BCS AST1
	LDX PANIK
	DEX
	CPX DEPTH
	BNE NPB
	INC STAVAL+1
NPB	CLC
	ADC #15
	LDX EINDSP
	BEQ ESP
	CLC
	ADC #30
ESP	LDX KLEUR
	BEQ ZAZ
WAZ	LDX KP1
	LDY BORD,X
	BMI NKV
	LDX IPTR
	STA TMP
	LDA MARK,X
	AND #$E
	BEQ NKV2
	BNE NKV3
ZAZ	LDX KP2
	LDY BORD,X
	BMI NKV
	LDX IPTR
	STA TMP
	LDA MARK,X
	AND #$E0
	BEQ NKV2
NKV3	LDA TMP
	SEC
	SBC #30
NKV	STA TMP
NKV2	LDA IPTR
	AND #$38
	STA YCNT
	EOR IPTR
	STA XCNT
	LDA XCNT
	SEC
	SBC #2
	BMI SP1
	JSR PTST
	BEQ SP2
SP1	LDA TMP
	SEC
	SBC #15
	STA TMP
SP2	LDA XCNT
	CLC
	ADC #2
	CMP #8
	BCS SP3
	JSR PTST
	BEQ SP4
SP3	LDA TMP
	SEC
	SBC #15
	STA TMP
SP4	LDA BPTR
	SEC
	SBC #8
	BMI PROM
	CMP IPTR
	BEQ SP5
	JSR PT0
	BNE SP5
	SEC
	LDA TMP
	SBC #40
	STA TMP
SP5	LDA BPTR
	CLC
	ADC #8
	CMP #64
	BCS PROM
	CMP IPTR
	BEQ SP6
	JSR PT0
	BNE SP6
	SEC
	LDA TMP
	SBC #40
	STA TMP
SP6	LDA TMP
	JMP ST1
PROM	LDA #7
	CLC
	ADC STAVAL+1
	STA STAVAL+1
	LDA #0
ST1	LDX BPTR
	CLC
	ADC POSVAL,X
	LDX IPTR
	SEC
	SBC POSVAL,X
ST2	STA TMP
	LDA OLDTV
	AND #$3F
	CMP IPTR
	BNE ST2B
	LDA STUK
	AND #7
	CMP #6
	BEQ ST2B
	CMP #3
	BCC ST2B
	LDA TMP
	SEC
	SBC #15
	STA TMP
ST2B	LDA KLEUR
	ASL
	CLC
	ADC BPTR
	SEC
	SBC #8
	TAY
	AND #$C0
	BNE ST3
	TYA
	CMP IPTR
	BEQ ST3
	JSR PT0
	BNE ST3
	LDA TMP
	SEC
	SBC #15
	STA TMP
ST3	LDA TMP
	BPL ST4
	DEC STAVAL+1
ST4	LDX TOSTUK
	CPX #2
	BCC  ST5
	CPX #8
	BEQ ST5
	BIT BONUS
	BPL ST4A
	INC STAVAL+1
ST4A	SBC BONUS
	BCS ST5
	DEC STAVAL+1
ST5	STA STAVAL
	LDA DEPTH
	BNE S1
	LDA RANDOM
	CLC
	ADC #$27
	STA RANDOM
	LSR
	LSR
	LSR
	LSR
	CLC
	ADC STAVAL
	STA STAVAL
	BCC S1
	INC STAVAL+1
S1	RTS
;*************************************************
;* BEPAAL DE WAARDE VAN HET STUK IN A, MET KLEUR!*
;*************************************************
WAARDE	AND #$1F
	CMP #$F
	PHP
	AND #$F
	CMP #8
	BNE W1
	LDA #1
W1	AND #7
	ASL
	TAX
	LDA WTAB,X
	STA STAVAL
	LDA WTAB+1,X
	PLP
	BCC W2
	LDA #2
W2	STA STAVAL+1
	RTS
;*************************************************
;* BEPAAL DE BEWEEGLIJKHEID VAN DE STUKKEN DOOR  *
;* HET AANTAL LEGALE ZETTEN TE TELLEN. (TZET).   *
;*************************************************
MTMOB	LDA #<TZET
	STA PROCES
	LDA #>TZET
	STA PROCES+1
	JSR SCAN3
	LDA ZETTEN
	CLC
	ADC MOB2
	ROR
	STA MOB2
	RTS
TZET	INC ZETTEN
	RTS
;*****************************************************
;* TEST OF OPGEWEKTE ZET = INGEVOERDE ZET EN ZET MODE*
;*****************************************************
ZTST	LDA IPTR
	CMP ZET
	BNE UNEQ
	LDA BPTR
	CMP ZET+1
	BNE UNEQ
	LDA MODE
	STA ZET+2
	INC OKFLG
UNEQ	INC ZETTEN
	RTS
;*************************************************
;* TEL PIONNEN, DAMES EN SNELLE STUKKEN. NOTEER  *
;* BOVENDIEN DE POSITIES VAN BEIDE KONINGEN EN   *
;* HOUDT HET AANTAL PIONNEN PER LIJN BIJ.        *
;*************************************************
KTEL	LDY #63
	LDA #1
	STA KFLG
	LDA #0
	STA DFLG
	STA PFLG
	STA SDIF
	LDX #7
KT0	STA TOREN,X
	DEX
	BPL KT0
KT1	LDA BORD,Y
	BEQ KT2
	AND #7
	CMP #6
	BNE KT4
	LDA BORD,Y
	JSR KPT
	JMP KT2
KT4	CMP #2
	BEQ KT5
	BCS KT3
	INC PFLG
	TYA
	AND #7
	TAX
	LDA TOREN,X
	SEC
	SBC #12
	STA TOREN,X
	JMP KT2
KT3	LDX #0
	STX KFLG
	CMP #5
	BNE KT5
	LDX #1
	STX DFLG
KT5	LDA BORD,Y
	AND #8
	CMP KLEUR
	BNE KT6
	DEC SDIF
	JMP KT2
KT6	INC SDIF
KT2	DEY
	BPL KT1
	RTS
;************************************************
;* BEREKEN SOORT AFSTAND TUSSEN BPTR EN LAST.   *
;************************************************
DIF	JSR SPLIT
	STX XCNT
	STY YCNT
	LDA LAST
	JSR SPLIT
	TXA
	SEC
	SBC XCNT
	JSR ABS
	STA XSAV
	TYA
	SEC
	SBC YCNT
	JSR ABS
	CMP XSAV
	BPL D1
	TAX
	LDA XSAV
	STX XSAV
D1	ASL
	ASL
	CLC
	ADC XSAV
	RTS
;**********************************************************
;* SPLITS ACCUMULATOR IN X EN Y COORDINAAT 9IN X EN Y REG)*
;**********************************************************
SPLIT	PHA
	AND #7
	TAX
	PLA
	LSR
	LSR
	LSR
	TAY
	RTS
;***********************************************
;* BEREKEN ABSOLUTE WAARDE VAN DE ACCUMULATOR. *
;***********************************************
ABS	CMP #0
	BPL AB1
	EOR #$FF
	CLC
	ADC #0
AB1	RTS
;*********************************************************
;* PRINT DE STRING WAAR X NAAR WIJST.                    *
;*********************************************************
P1	JSR OUTALL		;PRINT CHAR
	INX
PRT	LDA MES1,X
	BNE P1
	RTS
COMP	INC ZETTEN
	RTS
;*********************************************************
;* DEZE ROUTINE KIJKT WAT ER DOOR DE LAATSTE ZET (ELKE   *
;* ALS DE VORIGE ZET SCHAAK OF TERUGSLAAN WAS) IS IN     *
;* KOMEN TE STAAN. aLS EEN HOGER STUK DOOR EEN LAGER IS  *
;* AANGEVALLEN WORDT DE AANVALLER GENOTEERD IN AGRES,    *
;* EN DE AANGEVALLENE IN ALARM. BOVENDIEN WORDT DE       *
;* RICHTING VAN DE AANVAL IN LINE BEWAARD (I.V.M. TUSSEN *
;* ZETTEN). DE WAARDE VAN HET AANGEVALLEN STUK KOMT IN   *
;* EXTRA. ALS EEN LAGER STUK AANGEVALLEN WORDT DOOR EEN  *
;* EEN HOGER STUK CONTROLEREN WE OF HET VOLDOENDE GEDEKT *
;* STAAT, EN ZO NIET DAN NOTEREN WE WAT DE DREIGING OP   *
;* DEZELFDE MANIER, ALLEEN WORDT HET MSB VAN AGRES NU GE-*
;* ZET OM AAN TE GEVEN DAT DEKKEN MOGELIJK HELPT.        *
;*********************************************************
KIJK	LDA TOSTUK
	BEQ K1
	AND #7
	CMP #6
	BEQ K2
	LDX JPTR
	LDA GRP,X
	CMP MOVDIR
	BEQ K1
	TSX
	LDA $116,X
	CMP #>MSCHK
	BEQ K2
	LDA $113,X
	LSR
	BEQ K1A
	BCS K2
K1A	LDA CLASS
	AND #4
	BNE K2
	LDX IPTR
	CPX TV
	BNE K1
K2	LDA STUK
	AND #$F
	TAX
	LDA SCOD,X
	LDY BPTR
	AND MARK,Y
	BEQ K5
	LDA (DEKCNT),Y
	BMI K5
	BEQ K5
K5	LDA TOSTUK
	BEQ K1
	JSR WAARDE
	STA TMP
	LDA STUK
	JSR WAARDE
	CMP TMP
	LDA #0
	BCC K4
	LDY BPTR
	LDA (DEKCNT),Y
	BEQ K1
	BMI K1
	LDA #$80
K4	LDX TMP
	CPX EXTRA
	BMI K1
	BEQ K1
K3	STX EXTRA
	ORA IPTR
	STA AGRES
	LDX JPTR
	LDA XDIR,X
	CLC
	ADC YDIR,X
	STA LINE
	LDX BPTR
	STX ALARM
K1	RTS
;***********************************************************
;* DEZE ROUTINE HOUD BIJ IN HET ARRAY MARK OP WELKE VeLDEN *
;* SCHAAK GEGEVEN KAN WORDEN, EN DOOR WELK STUK. ELK BITJE *
;* IN EEN BYTE VAN MARK SYMBOLISEERT EEN STUK.             *
;***********************************************************
UPDAT	LDA MARK,X
	AND #$EE
	BEQ UD1
	JMP WIS
UD1	LDA MARK,Y
	AND #$EE
	BEQ UD2
	JMP MERK
UD2	RTS
WIS	LDX #63
	LDA #0
WI1	STA MARK,X
	DEX
	BPL WI1
MERK	LDY KP1
	LDA #1
	JSR PAARD
	LDY KP2
	LDA #$10
	JSR PAARD
	LDX #7
MK1	LDY KP1
	LDA #2
	JSR LIJN
	LDY KP2
	LDA #$20
	JSR LIJN
	DEX
	CPX #4
	BCS MK1
MK2	LDY KP1
	LDA #4
	JSR LIJN
	LDY KP2
	LDA #$40
	JSR LIJN
	DEX
	BPL MK2
	RTS
;***********************************************************
;* BRENG MERKTEKENS AAN IN ARRAY MARK IN HET PATROON VAN   *
;* EEN PAARDENSPRONG, OM AAN TE GEVEN WAAR PAARD SCHAAK KAN*
;* GEVEN. MERKTEKEN HANGT AF VAN KLEUR PAARD ($10 OF $01)  *
;***********************************************************
PAARD	STY TMP
	STA CODE
	LDY #7
PD0	LDA TMP
	CLC
	ADC RICHT+8,Y
	BMI PD2
	TAX
	CMP #64
	BCS PD2
	EOR TMP
	AND #4
	BEQ PD1
	LDA TMP
	EOR RICHT+8,Y
	AND #4
	BNE PD2
PD1	LDA MARK,X
	ORA CODE
	STA MARK,X
PD2	DEY
	BPL PD0
	RTS
;********************************************************
;* SCAN EEN LIJN AF OM VELDEN TE MERKEN WAAR SCHAAK     *
;* MOGELIJK IS DOOR SNEL LOPENDE STUKKEN.               *
;********************************************************
LIJN	STA CODE
LY0	TYA
	STY TMP
	CLC
	ADC RICHT,X
	BMI LY2
	TAY
	CMP #64
	BCS LY2
	EOR TMP
	AND #4
	BEQ LY1
	LDA TMP
	EOR RICHT,X
	AND #4
	BNE LY2
LY1	LDA MARK,Y
	ORA CODE
	STA MARK,Y
	LDA BORD,Y
	BEQ LY0
LY2	RTS
;************************************************
;* TEST OF OP VELD X EEN VIJANDELIJK STUK STAAT *
;************************************************
BUIT	LDA BORD,X
	AND #8
	CMP KLEUR
	BEQ BT1
	LDA BORD,X
	AND #7
	CMP #2
	RTS
BT1	CLC
	RTS
;*******************************************************
;* NOTEER POSITIES KONINGEN IN KP1 (WIT) EN KP2) ZWART *
;*******************************************************
KPT	AND #$F
	CMP #6
	BNE KPT1
	STY KP2
	JMP KPT2
KPT1	CMP #$E
	BNE KPT2
	STY KP1
KPT2	RTS
;***********************************************************
;* VERWISSEL DE TIJD VAN WIT EN ZWART, ZODAT DE TIJDEN VAN *
;* DE SPELER DIE AAN ZET IS ALTIJD IN ETYD EN MTYD STAAN.  *
;* ETYD IS DE WERKELIJK GEBRUIKTE TIJD IN SECONDEN, DEZE   *
;* MOET ONDER INTERRUPT BIJGEHOUDEN WORDEN. DIT IS HIER    *
;* NIET GEIMPLEMENTEERD OMDAT HET TE MACHINE AFHANKELIJK IS*
;* MTYD BEVAT DE TYD DIE TOT NU TOE GEBRUIKT MAG ZIJN      *
;* GEZIEN HET AANTAL GESPEELDE ZETTEN. ALS WE DE INTERRUPT *
;* WEGLATEN IS NIET MEER ZEKER DAT USURPATOR ZICH AAN DE   *
;* VEREISTE BEDENKTIJD HOUD, MAAR DAT IS OOK ALLES         *
;***********************************************************
WTYD	LDX #3
	LDA MTYD
	CLC
	ADC #TPZ
	STA MTYD
	BCC WT1
	INC MTYD+1
WT1	SEI
WTL	LDA ETYD,X
	PHA
	LDA ETYD+4,X
	STA ETYD,X
	PLA
	STA ETYD+4,X
	DEX
	BPL WTL
	CLI
	RTS	
;*************************************************
;* PRINT STATUS INFORMATIE TIJDENS DENKEN.       *
;*************************************************
STATUS	JSR CRLF	;NIEUWE REGEL
	LDA ZETTEN
	JSR NUMA
	JSR BLANK
	LDA IPTR
	LDX BPTR
	JSR WRAX
	JSR BLANK
	LDA MAXVAL+1
	LDX MAXVAL
	JSR WRAX
	JSR BLANK
	LDA MAXCOS
	JSR NUMA
	JSR BLANK
	LDA $16FF
	JSR NUMA
	RTS
;********************************************************
;* INITIALISEER DE VERIABELEN IN PAGE ZERO.             *
;* DIT IS EEN APARTE ROUTINE OM B.V. TIJDENS HET ASSEM- *
;* BLEREN GEEN DATA IN PAGE ZERO TE HOEVEN PLAATSEN.    *
;* (VEEL ASSEMBLERS GEBRUIKEN DEZE PAGINA).             *
;********************************************************
INIT	LDX #0
	CLD
	STX PASFLG
	STX DEPTH
	STX COST
	STX OMODE
	STX KFLG
	STX MVELD
	STX DREIG
	STX DEKCNT
	DEX
	STX IPTR
	STX OLDSV
	STX BPTR
	STX OLDTV
	LDX #1
	STX NR
	LDX #9
	STX MAXDEP
	LDX #9
	STX MAXCOS
	LDX #8
	STX KLEUR
	LDX #<SCHAAK
	STX LIMIT
	LDX #>SCHAAK
	STX LIMIT+1
	LDX #64
	STX MOB
	STX MOB2
	LDX #$14
	STX DEKCNT+1
	LDX #7
ILP	LDA BEG,X
	ORA #$80
	STA BORD,X
	ORA #8
	STA BORD+$38,X
	LDA #$81
	STA BORD+8,X
	LDA #$88
	STA BORD+$30,X
	LDA #0
	STA BORD+$10,X
	STA BORD+$18,X
	STA BORD+$20,X
	STA BORD+$28,X
	STA ETYD,X
	DEX
	BPL ILP
	LDX #7
	STX MTYD+1
	STX MTYD+5
	CLI
	RTS
ETEXT	=   *
;************************************************************
;* TABELLEN:                                                *
;* RANGE        GEEFT AAN HOE VER ALLE STUKKEN IN EEN       *
;*              BEPAALDE RICHTING MOGEN ZETTEN.             *
;* XDIR&YDIR    GEVEN DE X EN Y COORDINAAT VAN DE RICHTING  *
;*              VECTOR.                                     *
;* GRP          GEEFT AAN IN WELKE GROEP EEN RICHTING HOORT *
;* PRICE        GEEFT AAN HOEDUUR ZETTEN VAN TYPE CLASS ZIJN*
;* WTAB         GEEFT DE WAARDE DER STUKKEN.                *
;* BEG          DE BEGINOPSTELLING DER STUKKEN.             *
;* RICHT        NOGMAALS DE RICHTINGEN, NU GECOMBINEERD     *
;* SCOD         GEEFT AAN IN WELKE RICHTINGEN DE STUKKEN    *
;*              SCHAAK KUNNEN GEVEN (MAX CODES).            *
;************************************************************
	*=  $1300
RANGE	.BYTE 0,0,$FF,$11,0,$FB,$21,$21,0,0,0,0,0,0,0,$F7
	.BYTE 0,$11,0,$ED,$21,$21,0,0,0,0,0,0,0,0,0,$E9
	.BYTE 0,0,0,0,0,0,0,$DF,1,1,1,1,1,1,1,1
	.BYTE 0,0,0,$CF,7,7,7,7,0,0,0,0,0,0,0,$C7
	.BYTE 7,7,7,7,0,0,0,0,0,0,0,0,0,0,0,$BB
	.BYTE 7,7,7,7,7,7,7,7,0,0,0,0,0,0,0,$A7
	.BYTE $41,1,$41,1,1,1,1,1,0,0,0,0,0,0,0,$97
XDIR	.BYTE 1,0,$FF,0,1,$FF,$FF,1,2,1,$FF,$FE,$FE,$FF,1,2
YDIR	.BYTE 0,8,0,$F8,8,8,$F8,$F8,8,16,16,8,$F8,$F0,$F0,$F8
GRP	.BYTE 0,1,0,1,2,3,2,3,4,4,4,4,4,4,4,4
PRICE	.BYTE 4,3,4,0,4,0,4,0,4,0,4,0,4,0,4,0
	.BYTE 8,0,8,0,8,0,8,0,8,0,8,0,8,0,8,0
WTAB	.WORD 0,$100,$300,$300,$500,$900,SCHAAK,0
BEG	.BYTE 4,2,3,5,6,3,2,4
RICHT	.BYTE 1,8,$FF,$F8,9,7,$F7,$F9
	.BYTE 10,17,15,6,$F6,$EF,$F1,$FA
SCOD	.BYTE 0,0,1,2,4,6,0,0
	.BYTE 0,0,$10,$20,$40,$60,0,0
EDATA	=   *
	*=  $13FA
	.DBYTE EZERO,ETEXT,EDATA
	.END